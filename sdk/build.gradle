plugins {
	id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

group = 'org.opencv'
version = rootProject.ext.opencv4AndroidVersion

def openCVersionName = '4.8.1'
def openCVersionCode = ((4 * 100 + 8) * 100 + 1) * 10 + 0

println "OpenCV: " +openCVersionName + " " + project.buildscript.sourceFile

android {
    namespace 'org.opencv'
    compileSdk 33
    ndkVersion gradle.ndkVersion
    defaultConfig {
        minSdk 21
        targetSdk 33

        versionCode openCVersionCode
        versionName openCVersionName

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
                targets 'opencv_jni_shared'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    buildFeatures{
        aidl true
        buildConfig true
    }

    buildTypes {
        debug {
            packagingOptions {
                doNotStrip '**/*.so'  // controlled by OpenCV CMake scripts
            }
        }
        release {
            packagingOptions {
                doNotStrip '**/*.so'  // controlled by OpenCV CMake scripts
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['native/libs']
            java.srcDirs = ['java/src']
            aidl.srcDirs = ['java/src']
            res.srcDirs = ['java/res']
            manifest.srcFile 'java/AndroidManifest.xml'
        }
    }

    externalNativeBuild {
        cmake {
            path (project.projectDir.toString() + '/libcxx_helper/CMakeLists.txt')
            version gradle.cmakeVersion
        }
    }
}

dependencies {
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'org.opencv'
                artifactId = 'opencv4android'
                version = rootProject.ext.opencv4AndroidVersion
            }
        }
    }
}